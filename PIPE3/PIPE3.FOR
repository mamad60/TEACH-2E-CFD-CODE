C     PROGRAM MAIN
C     SUBROUTINE CONTRO
C
CHAPTER  0  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/UVEL/RESORU,NSWPU,URFU,DXEPU(IZ),DXPWU(IZ),SEWU(IZ)
     1/VVEL/RESORV,NSWPV,URFV,DYNPV(IZ),DYPSV(IZ),SNSV(IZ),RCV(IZ)
     1/PCOR/RESORM,NSWPP,URFP,DU(IZ,IZ),DV(IZ,IZ),IPREF,JPREF
     1/TEMPC/RESORT,URFT,ZERO,NSWPT
     1/TEN/RESORK,NSWPK,URFK
     1/TDIS/RESORE,NSWPD,URFE
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/GEOM/INDCOS,X(IZ),Y(IZ),DXEP(IZ),DXPW(IZ),DYNP(IZ),DYPS(IZ),
     1      SNS(IZ),SEW(IZ),XU(IZ),YV(IZ),R(IZ),RV(IZ)
      COMMON
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/KASE T1/UIN,TEIN,EDIN,FLOWIN,ALAMDA,
     2         RSMALL,DIAM,AL1,AL2,JSTEP,ISTEP,JSTP1,JSTM1,ISTP1,ISTM1
     1/CONCEN/RESORC,URFC,NSWPC
     1/TURB/GEN(IZ,IZ),CD,CMU,C1,C2,CAPPA,ELOG,PRED,PRTE
     1/WALLF/YPLUSN(IZ),XPLUSW(IZ),TAUN(IZ),TAUW(IZ)
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
      CHARACTER*6 HEDU(6),HEDV(6),HEDP(6),HEDT(6),HEDK(6),HEDD(6),
     1HEDM(6),HEDA(6),HEDB(6),HEDC(6),HEDE(6)
      LOGICAL INCALU,INCALV,INCALP,INCALT,INPRO,INCALK,INCALD,INCALC
     1       ,INCALE
      DATA HEDU/'U VE','LOCI','TY',' ',' ',' '/
      DATA HEDE/'D DE','NSIT','Y ',' ',' ',' '/
      DATA HEDC/'C CO','NCEN','TRAT','ION ',' ',' '/
      DATA HEDV/'V VE','LOCI','TY',' ',' ',' '/
      DATA HEDP/'PRES','SURE',' ',' ',' ',' '/
      DATA HEDT/'TEMP','ERAT','URE',' ',' ',' '/
      DATA HEDK/'TURB','ULEN','CE E','NERG','Y',' '/
      DATA HEDD/'DISS','IPAT','ION',' ',' ',' '/
      DATA HEDM/'VISC','OSIT','Y',' ',' ',' '/
      DATA HEDA/'KPLU','S TE',' RHO',' TAU','N',' '/
      DATA HEDB/'LENG','TH S','CALE',' PIP','E RA','D'/
C
      OPEN(UNIT=8,FILE='OUTPUT.DAT')
      ZERO=0.0
      GREAT=1.E30
      ISTEP=2
      JSTEP=2
      NITER=0
      NITT=0
      IT=20
      JT=20
      NSWPU=3
      NSWPV=3
      NSWPP=5
      NSWPT=5
      NSWPK=3
      NSWPD=3
C
CHAPTER  1  1  1  1  1  PARAMETERS AND CONTROL INDICES  1  1  1  1  1  1
C
      INDCOS=2
C-----------------
      PRINT*,'PROGRAM STARTING.'
      NI=20
      NJ=20
C-----GRID
      NIM1=NI-1
      NJM1=NJ-1
C-----------------------------------------------------
C-------DIAMETER OF PIPE------
      DIAM=0.01
C-------LENGTH OF PIPE--------
      ALTOT=1.0
      DX=ALTOT/FLOAT(NI-2)
      X(1)=-0.5*DX
      X(2)=-X(1)
      DO 100 I=3,NI
      X(I)=X(I-1)+DX
100   CONTINUE
C----------- Y-AXIS------------------------
      DY=DIAM/FLOAT(NJ-2)
      Y(1)=-0.5*DY
      Y(2)=-Y(1)
      DO 101 J=3,NJ
      Y(J)=Y(J-1)+DY
101   CONTINUE
C------PLOT MESH  ---------------
C-----GEOMETRICAL AND THERMAL PARAMETERS
C-----DEPENDENT VARIABLE SELECTION
      INCALU=.TRUE.
      INCALV=.TRUE.
      INCALP=.TRUE.
      INCALT=.TRUE.
      INCALK=.FALSE.
      INCALD=.FALSE.
      INPRO=.TRUE.
C-----FLUID PROPERTIES
      DENSIT=1.16
      GRAV=9.8
      PRANDT=1.0
      VISCOS=184.6E-7
C-----TURBULENCE CONSTANTS
      CMU=0.09
      CD=1.0
      C1=1.44
      C2=1.92
      CAPPA=0.4187
      ELOG=9.793
      PRED=CAPPA*CAPPA/(C2-C1)/(CMU**0.5)
      PRTE=1.0
C-----BOUNDARY VALUES
      UIN=2.0
      TURBIN=0.03
      TEIN=TURBIN*UIN**2.
      ALAMDA=0.005
      EDIN=TEIN**1.5/(ALAMDA*DIAM)
      GASTE=373.0
      TEBOU=293.0
C-----PRESSURE CALCULATION
      IPREF=2
      JPREF=2
C-----PROGRAM CONTROL AND MONITOR
      MAXIT=3000
C---- MONITOR POINTS -------
      IMON=5
      JMON=5
C---- UNDER RELAXATION FACTORS----------
      URFU=0.5
      URFV=0.5
      URFP=1.0
      URFT=0.7
      URFE=0.3
      URFK=0.3
      URFVIS=0.7
C--- THESE PARAMETERS ARE TO CONTROL NUMBAR OF ITERATIONS ---
      INDPRI =100
      SORMAX =0.05
      SORMA1 =0.001
C
C
CHAPTER  2  2  2  2  2  2  INITIAL OPERATIONS  2  2  2  2  2  2  2  2  2
C
C-----CALCULATE GEOMETRICAL QUANTITIES AND SET VARIABLES TO ZERO
      CALL INIT
C-----INITIALISE VARIABLE FIELDS
      FLOWIN=0.0
      ARDEN=0.0
C-----INLET VARIABLES
      DO 200 J=2,NJM1
      U(2,J)=UIN
      TE(1,J)=TEIN
      ED(1,J)=EDIN
      T(1,J)=GASTE
  200 CONTINUE
C-----MAIN FIELD
      PRESS=1.0E5
      DO 202 I=2,NIM1
      DO 202 J=2,NJM1
      TE(I,J)=TEIN
      ED(I,J)=EDIN
      P(I,J)=PRESS
 202  U(I,J)=UIN
      ITEMP=NI/2
      DO 800 I=1,ITEMP
      DO 800 J=1,NJ
  800 T(I,J)=GASTE
      DO 801 I=ITEMP,NI
      DO 801 J=1,NJ
  801 T(I,J)=TEBOU
C-----TOP WALL
      DO 203 I=2,NIM1
 203  YPLUSN(I)=11.0
      CALL PROPS
C------INITIAL OUTPUT
      WRITE(8,210)
      WRITE(8,211)
      WRITE(8,220)UIN
      RE=UIN*DENSIT*2.*DIAM/VISCOS
      WRITE(8,230)RE
C      WRITE(8,240)RSDRL
      WRITE(8,260) DENSIT
      WRITE(8,250) VISCOS
C      IF(INCALU) CALL PRINT(2,2,NI,NJ,IT,JT,XU,Y,U,HEDU)
C      IF(INCALV) CALL PRINT(2,2,NI,NJ,IT,JT,X,YV,V,HEDV)
C      IF(INCALP) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,P,HEDP)
C      IF(INCALT) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,T,HEDT)
C      IF(INCALK) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,TE,HEDK)
C      IF(INCALD) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,ED,HEDD)
C.......CALCULATE RESIDUAL SOURCES NORMALIZATION FACTORS................
      FLOWIN=0.0
      XMONIN=0.0
      TMONIN=0.0
      DO 657 J=2,NJM1
      ARDEN=0.5*(DEN(1,J)+DEN(2,J))*R(J)*SNS(J)
      FLOWIN=FLOWIN+ARDEN*U(2,J)
      XMONIN=XMONIN+ARDEN*U(2,J)*U(2,J)
      TMONIN=TMONIN+ARDEN*U(2,J)*GASTE
  657 CONTINUE
C
CHAPTER 3 3 3 3 3 3  TIME LOOP + ITERATION LOOP 3 3 3 3 3 3 3 3
C
      WRITE(8,310)IMON,JMON
  300 NITER=NITER+1
      IF(MOD(NITER,20).EQ.0) WRITE(8,550) NITER
  550 FORMAT(10X,'NITER= ',I3)
C-----COMPUTE OTHER VARIABLES
      IF(INCALU) CALL CALCU
      IF(INCALV) CALL CALCV
      IF(INCALP) CALL CALCP
      IF(INCALK) CALL CALCTE
      IF(INCALD) CALL CALCED
C-----UPDATE FLUID PROPERITIES
      IF(INPRO) CALL PROPS
C-----INTERMEDIATE OUTPUT
      RESORM =RESORM/FLOWIN
      RESORU =RESORU/XMONIN
      RESORV =RESORV/XMONIN
      RESORT =RESORT/TMONIN
      WRITE(8,311) NITER,RESORU,RESORV,RESORM,RESORT,RESORK,RESORE
     1,U(IMON,JMON),V(IMON,JMON),P(IMON,JMON)
     2,T(IMON,JMON),TE(IMON,NJM1),ED(IMON,NJM1)
      IF(MOD(NITER,INDPRI).NE.0) GO TO 309
C      IF(INCALU) CALL PRINT(2,2,NI,NJ,IT,JT,XU,Y,U,HEDU)
      OPEN(UNIT=5,FILE='PLOTU.PLT')
      OPEN(UNIT=6,FILE='PLOTV.PLT')
      OPEN(UNIT=12,FILE='PLOTP.PLT')
      OPEN(UNIT=9,FILE='PLOTK.PLT')
      OPEN(UNIT=10,FILE='PLOTE.PLT')
      OPEN(UNIT=11,FILE='PLOTT.PLT')
      DO 704 I=1,NI
      DO 704 J=1,NJ
      WRITE(5,702)U(I,J)
      WRITE(6,702)V(I,J)
      WRITE(12,702)P(I,J)
      WRITE(9,702)TE(I,J)
      WRITE(10,702)ED(I,J)
      WRITE(11,702)T(I,J)
  704 CONTINUE
      CLOSE (5)
      CLOSE (6)
      CLOSE (12)
      CLOSE (9)
      CLOSE (10)
      CLOSE (11)
C      IF(INCALV) CALL PRINT(2,2,NI,NJ,IT,JT,X,YV,V,HEDV)
C      IF(INCALP) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,P,HEDP)
C      IF(INCALT) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,T,HEDT)
C      IF(INCALK) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,TE,HEDK)
C      IF(INCALD) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,ED,HEDD)
      WRITE(8,310)IMON,JMON
  309 CONTINUE
C-----TERMINATION TESTS
      SORCE  =AMAX1(RESORM,RESORU,RESORV)
      WRITE(*,501)NITER,SORCE,RESORM,RESORU,RESORV,RESORT
      IF(NITER.EQ.40.AND.SORCE.GT.1.0E4*SORMAX) GOTO 302
      IF(NITER.GT.MAXIT) GOTO 302
      IF(SORCE.GT.SORMAX) GOTO 300
  302 CONTINUE
      NITT=NITT+1
      IF(INCALT) CALL CALCT
      IF(MOD(NITT,INDPRI).NE.0)GOTO 1001
C      IF(INCALT) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,T,HEDT)
      WRITE(8,1100)RESORT
 1100 FORMAT(1X,'TEMP SORCE ',E15.5)
 1001 SORCT=RESORT
      IF(NITT.EQ.MAXIT)GOTO 1002
      IF(SORCT.GT.SORMA1) GOTO 302
CHAPTER 4 4 4 4 4 4 4 4 FINAL OPERATIONS AND OUTPUT 4 4 4 4 4 4 4 4 4 4
 1002 CONTINUE
C 1002 IF(INCALU) CALL PRINT(2,2,NI,NJ,IT,JT,XU,Y,U,HEDU)
C      IF(INCALV) CALL PRINT(2,2,NI,NJ,IT,JT,X,YV,V,HEDV)
C      IF(INCALP) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,P,HEDP)
C      IF(INCALT) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,T,HEDT)
C      IF(INCALK) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,TE,HEDK)
C      IF(INCALD) CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,ED,HEDD)
      OPEN(UNIT=6,FILE='PLOTV.PLT')
      OPEN(UNIT=5,FILE='PLOTU.PLT')
      OPEN(UNIT=12,FILE='PLOTP.PLT')
      OPEN(UNIT=9,FILE='PLOTK.PLT')
      OPEN(UNIT=10,FILE='PLOTE.PLT')
      OPEN(UNIT=11,FILE='PLOTT.PLT')
      DO 710 I=1,NI
      DO 710 J=1,NJ
      WRITE(5,702)U(I,J)
      WRITE(6,702)V(I,J)
      WRITE(12,702)P(I,J)
      WRITE(9,702)TE(I,J)
      WRITE(10,702)ED(I,J)
      WRITE(11,702)T(I,J)
  710 CONTINUE
      CLOSE (5)
      CLOSE (6)
      CLOSE (12)
      CLOSE (9)
      CLOSE (10)
      CLOSE (11)
C      IF(INPRO)  CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,VIS,HEDM)
C
C
C-----CALCULATION OF NON DIMENSIONAL TURBULENCE ENERGY AND LENGTH SCALE
C      IF(.NOT.INCALK) GO TO   500
C      DO 400 I=2,NIM1
C      DO 400 J=2,NJM1
C      SU(I,J)=TE(I,J)*DEN(I,J)/ABS(TAUN(I))
C 400  SP(I,J)=TE(I,J)**1.5/ED(I,J)/DIAM
C      CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,SU,HEDA)
C      CALL PRINT(2,2,NI,NJ,IT,JT,X,Y,SP,HEDB)
C  500 CONTINUE
C-----CALCULATION OF SHEAR-STRESS COEFFICIENT ALONG DUCT WALL-----------
C      WRITE(8,402)
C      DO 401 I=ISTEP,NIM1
C      SSC=TAUN(I)/(1.0*DENSIT*ULARGE*ULARGE)
C      XUH=XU(I)/(DIAM-RSMALL)
C      WRITE(8,403)I,XUH,SSC
C  401 CONTINUE
  702 FORMAT(E15.5)
      CLOSE (8)
C-----------------------------------------------------------
      STOP
C-----FORMAT STATEMENTS
  210 FORMAT(1H0,'FLOW THROUGH A HEATED PIPE'////)
  211 FORMAT(////)
  220 FORMAT(//1H0,15X,'INLET FLUID VELOCITY ',T60,1H=,3X,E11.3)
  230 FORMAT(1H0,15X,'REYNOLDS NUMBER ',T60,1H=,3X,E11.3)
  240 FORMAT(1H0,15X,'DIAMETER RATIO ',T60,1H=,3X,E11.3)
  250 FORMAT(1H0,15X,'LAMINAR VISCOSITY ',T60,1H=,3X,E11.3)
  260 FORMAT(1H0,15X,'FLUID DENSITY ',T60,1H=,3X,E11.3)
  310 FORMAT(1H0,'ITER   ','I-----ABSOLUTE RESIDUAL SOURCE SUM
     1S------I   I----FIELD VALUES AT MONITORING LOCATION','
     2(',I2,',',I2,')','--------I' / 2X,'NO.',3X,'UMOM',6X,'VMOM',6X,'MA
     3SS',6X,'ENER',6X,'TKIN',6X,'DISP',10X,'U',9X,'V',9X,'P',9X,'T',9X,
     4'K',9X,'D'/)
311   FORMAT(1H0,I3,4X,1P6E10.3,3X,1P6E10.3)
402   FORMAT(///5X,1HI,7X,5HXU(I),6X,10HS.S.COEFF.)
403   FORMAT(/5X,I5,2(1PE11.3))
501   FORMAT(1H ,I3,' SOURCE = ',5F10.4)
      END
	    SUBROUTINE CALCED
C
CHAPTER  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/TDIS/RESORE,NSWPD,URFE
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/GEOM/INDCOS,X(IZ),Y(IZ),DXEP(IZ),DXPW(IZ),DYNP(IZ),DYPS(IZ),
     1      SNS(IZ),SEW(IZ),XU(IZ),YV(IZ),R(IZ),RV(IZ)
      COMMON
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/KASE T1/UIN,TEIN,EDIN,FLOWIN,ALAMDA,
     2         RSMALL,DIAM,AL1,AL2,JSTEP,ISTEP,JSTP1,JSTM1,ISTP1,ISTM1
     1/TURB/GEN(IZ,IZ),CD,CMU,C1,C2,CAPPA,ELOG,PRED,PRTE
     1/WALLF/YPLUSN(IZ),XPLUSW(IZ),TAUN(IZ),TAUW(IZ)
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
     1/SUSP/SUKD(IZ,IZ),SPKD(IZ,IZ)
C
CHAPTER  1  1  1  1  1  1  ASSEMBLY OF COEFFICIENTS  1  1  1  1  1  1
C
      PRINT*,'CALED'
      DO 100 I=2,NIM1
      DO 101 J=2,NJM1
C-----COMPUTE AREAS AND VOLUME
      AREAN=RV(J+1)*SEW(I)
      AREAS=RV(J)*SEW(I)
      AREAEW=R(J)*SNS(J)
      VOL=R(J)*SNS(J)*SEW(I)
C-----CALCULATE CONVECTION COEFFICIENTS
      GN=0.5*(DEN(I,J)+DEN(I,J+1))*V(I,J+1)
      GS=0.5*(DEN(I,J)+DEN(I,J-1))*V(I,J)
      GE=0.5*(DEN(I,J)+DEN(I+1,J))*U(I+1,J)
      GW=0.5*(DEN(I,J)+DEN(I-1,J))*U(I,J)
      CN=GN*AREAN
      CS=GS*AREAS
      CE=GE*AREAEW
      CW=GW*AREAEW
C-----CALCULATE DIFFUSION COEFFICIENTS
      GAMN=0.5*(VIS(I,J)+VIS(I,J+1))/PRED
      GAMS=0.5*(VIS(I,J)+VIS(I,J-1))/PRED
      GAME=0.5*(VIS(I,J)+VIS(I+1,J))/PRED
      GAMW=0.5*(VIS(I,J)+VIS(I-1,J))/PRED
      DN=GAMN*AREAN/DYNP(J)
      DS=GAMS*AREAS/DYPS(J)
      DE=GAME*AREAEW/DXEP(I)
      DW=GAMW*AREAEW/DXPW(I)
C-----SOURCE TERMS
      SMP=CN-CS+CE-CW
      ZERO=0.0
      CP=AMAX1(ZERO,SMP)
      CPO=CP
C-----ASSEMBLE MAIN COEFFICIENTS
      AN(I,J)=AMAX1(ABS(0.5*CN),DN)-0.5*CN
      AS(I,J)=AMAX1(ABS(0.5*CS),DS)+0.5*CS
      AE(I,J)=AMAX1(ABS(0.5*CE),DE)-0.5*CE
      AW(I,J)=AMAX1(ABS(0.5*CW),DW)+0.5*CW
      SU(I,J)=CPO*ED(I,J)
      SUKD(I,J)=SU(I,J)
      SU(I,J)=SU(I,J)+C1*CMU*GEN(I,J)*VOL*DEN(I,J)*TE(I,J)/VIS(I,J)
      SP(I,J)=-CP
      SPKD(I,J)=SP(I,J)
      SP(I,J)=SP(I,J)-C2*DEN(I,J)*ED(I,J)*VOL/TE(I,J)
  101 CONTINUE
  100 CONTINUE
C
CHAPTER  2  2  2  2  2  2  PROBLEM MODIFICATIONS  2  2  2  2  2  2
C
      CALL MODED
C
CHAPTER 3 FINAL COEFFICIENT ASSEMBLY AND RESIDUAL SOURCE CALCULATION 3
C
      RESORE=0.0
      DO 300 I=2,NIM1
      DO 301 J=2,NJM1
      AP(I,J)=AN(I,J)+AS(I,J)+AE(I,J)+AW(I,J)-SP(I,J)
      RESOR=AN(I,J)*ED(I,J+1)+AS(I,J)*ED(I,J-1)+AE(I,J)*ED(I+1,J)
     1      +AW(I,J)*ED(I-1,J)-AP(I,J)*ED(I,J)+SU(I,J)
      VOL=R(J)*SEW(I)*SNS(J)
      SORVOL=GREAT*VOL
      IF(-SP(I,J).GE.0.5*SORVOL) RESOR=RESOR/SORVOL
      RESORE=RESORE+ABS(RESOR)
C-----UNDER-RELAXATION
      AP(I,J)=AP(I,J)/URFE
      SU(I,J)=SU(I,J)+(1.-URFE)*AP(I,J)*ED(I,J)
  301 CONTINUE
  300 CONTINUE
C
CHAPTER  4  4  4  4  4  SOLUTION OF DIFFERENCE EQUATIONS  4  4  4  4  4
C
      DO 400 N=1,NSWPD
  400 CALL LISOLV(2,2,NI,NJ,IT,JT,ED)
      RETURN
      END
C----------------------------
      SUBROUTINE CALCP
C
CHAPTER  0  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/PCOR/RESORM,NSWPP,URFP,DU(IZ,IZ),DV(IZ,IZ),IPREF,JPREF
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/GEOM/INDCOS,X(IZ),Y(IZ),DXEP(IZ),DXPW(IZ),DYNP(IZ),DYPS(IZ),
     1      SNS(IZ),SEW(IZ),XU(IZ),YV(IZ),R(IZ),RV(IZ)
      COMMON
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
      RESORM=0.0
C
CHAPTER  1  1  1  1  1  1  ASSEMBLY OF COEFFICIENTS  1  1  1  1  1  1  1
C
      DO 100 I=2,NIM1
      DO 101 J=2,NJM1
C-----COMPUTE AREAS AND VOLUME
      AREAN=RV(J+1)*SEW(I)
      AREAS=RV(J)*SEW(I)
      AREAEW=R(J)*SNS(J)
      VOL=R(J)*SNS(J)*SEW(I)
C-----CALCULATE COEFFICIENTS
      DENN=0.5*(DEN(I,J)+DEN(I,J+1))
      DENS=0.5*(DEN(I,J)+DEN(I,J-1))
      DENE=0.5*(DEN(I,J)+DEN(I+1,J))
      DENW=0.5*(DEN(I,J)+DEN(I-1,J))
      AN(I,J)=DENN*AREAN*DV(I,J+1)
      AS(I,J)=DENS*AREAS*DV(I,J)
      AE(I,J)=DENE*AREAEW*DU(I+1,J)
      AW(I,J)=DENW*AREAEW*DU(I,J)
C-----CALCULATE SOURCE TERMS
      CN=DENN*V(I,J+1)*AREAN
      CS=DENS*V(I,J)*AREAS
      CE=DENE*U(I+1,J)*AREAEW
      CW=DENW*U(I,J)*AREAEW
      SMP=CN-CS+CE-CW
      SP(I,J)=0.
      SU(I,J)=-SMP
C-----COMPUTE SUM OF ABSOLUTE MASS SOURCES
      RESORM=RESORM+ABS(SMP)
  101 CONTINUE
  100 CONTINUE
C
CHAPTER  2  2  2  2  2  2  2  PROBLEM MODIFICATIONS  2  2  2  2  2  2  2
C
      CALL MODP
C
CHAPTER  3  3  3  3  3  FINAL COEFFICIENT ASSEMBLY  3  3  3  3  3  3  3
C
      DO 300 I=2,NIM1
      DO 301 J=2,NJM1
  301 AP(I,J)=AN(I,J)+AS(I,J)+AE(I,J)+AW(I,J)-SP(I,J)
  300 CONTINUE
C
CHAPTER  4  4  4  4  4  SOLUTION OF DIFFERENCE EQUATIONS  4  4  4  4  4
C
C
CHAPTER  5  5  5  5  CORRECT VELOCITIES AND PRESSURE  5  5  5  5  5  5
C
      DO 400 N=1,NSWPP
  400 CALL LISOLV(2,2,NI,NJ,IT,JT,PP)
C
C-----VELOCITIES
      DO 500 I=2,NIM1
      DO 501 J=2,NJM1
      IF(I.NE.2) U(I,J)=U(I,J)+DU(I,J)*(PP(I-1,J)-PP(I,J))
      IF(J.NE.2) V(I,J)=V(I,J)+DV(I,J)*(PP(I,J-1)-PP(I,J))
  501 CONTINUE
  500 CONTINUE
C-----PRESSURES (WITH PROVISION FOR UNDER-RELAXATION)
      PPREF=PP(IPREF,JPREF)
      DO 502 I=2,NIM1
      DO 503 J=2,NJM1
      P(I,J)=P(I,J)+URFP*(PP(I,J)-PPREF)
      PP(I,J)=0.0
  503 CONTINUE
  502 CONTINUE
      RETURN
      END
      SUBROUTINE CALCT
C
CHAPTER  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/TEMPC/RESORT,URFT,ZERO,NSWPT
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/GEOM/INDCOS,X(IZ),Y(IZ),DXEP(IZ),DXPW(IZ),DYNP(IZ),DYPS(IZ),
     1      SNS(IZ),SEW(IZ),XU(IZ),YV(IZ),R(IZ),RV(IZ)
      COMMON
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
C
CHAPTER  1  1  1  1  1  1  ASSEMBLY OF COEFFICIENTS  1  1  1  1  1  1
C
      DO 100 I=2,NIM1
      DO 101 J=2,NJM1
C-----COMPUTE AREAS AND VOLUME
      AREAN=RV(J+1)*SEW(I)
      AREAS=RV(J)*SEW(I)
      AREAEW=R(J)*SNS(J)
      VOL=R(J)*SNS(J)*SEW(I)
C-----CALCULATE CONVECTION COEFFICIENTS
      GN=0.5*(DEN(I,J)+DEN(I,J+1))*V(I,J+1)
      GS=0.5*(DEN(I,J)+DEN(I,J-1))*V(I,J)
      GE=0.5*(DEN(I,J)+DEN(I+1,J))*U(I+1,J)
      GW=0.5*(DEN(I,J)+DEN(I-1,J))*U(I,J)
      CN=GN*AREAN
      CS=GS*AREAS
      CE=GE*AREAEW
      CW=GW*AREAEW
C-----CALCULATE DIFFUSION COEFFICIENTS
      GAMN=0.5*(GAMH(I,J)+GAMH(I,J+1))
      GAMS=0.5*(GAMH(I,J)+GAMH(I,J-1))
      GAME=0.5*(GAMH(I,J)+GAMH(I+1,J))
      GAMW=0.5*(GAMH(I,J)+GAMH(I-1,J))
      DN=GAMN*AREAN/DYNP(J)
      DS=GAMS*AREAS/DYPS(J)
      DE=GAME*AREAEW/DXEP(I)
      DW=GAMW*AREAEW/DXPW(I)
C-----SOURCE TERMS
      SMP=CN-CS+CE-CW
      CP=AMAX1(ZERO,SMP)
      CPO=CP
C-----ASSEMBLE MAIN COEFFICIENTS
      AN(I,J)=AMAX1(ABS(0.5*CN),DN)-0.5*CN
      AS(I,J)=AMAX1(ABS(0.5*CS),DS)+0.5*CS
      AE(I,J)=AMAX1(ABS(0.5*CE),DE)-0.5*CE
      AW(I,J)=AMAX1(ABS(0.5*CW),DW)+0.5*CW
      SU(I,J)=CPO*T(I,J)
      SP(I,J)=-CP
  101 CONTINUE
  100 CONTINUE
C
CHAPTER  2  2  2  2  2  2  PROBLEM MODIFICATIONS  2  2  2  2  2  2
C
      CALL MODT
C
CHAPTER 3 FINAL COEFFICIENT ASSEMBLY AND RESIDUAL SOURCE CALCULATION 3
C
      RESORT=0.0
      DO 300 I=2,NIM1
      DO 301 J=2,NJM1
      AP(I,J)=AN(I,J)+AS(I,J)+AE(I,J)+AW(I,J)-SP(I,J)
C      WRITE(*,503)I,J
C      WRITE(*,502)AN(I,J),AS(I,J),AE(I,J),AW(I,J),SP(I,J)
C  502 FORMAT(1X,5E15.5)
C  503 FORMAT(1X,2I4)
      RESOR=AN(I,J)*T(I,J+1)+AS(I,J)*T(I,J-1)+AE(I,J)*T(I+1,J)
     1      +AW(I,J)*T(I-1,J)-AP(I,J)*T(I,J)+SU(I,J)
      VOL=R(J)*SEW(I)*SNS(J)
      SORVOL=GREAT*VOL
      IF(-SP(I,J).GT.0.5*SORVOL) RESOR=RESOR/SORVOL
      RESORT=RESORT+ABS(RESOR)
C-----UNDER-RELAXATION
      AP(I,J)=AP(I,J)/URFT
      SU(I,J)=SU(I,J)+(1.-URFT)*AP(I,J)*T(I,J)
C      WRITE(*,501)I,J,AP(I,J)
C  501 FORMAT(5X,2I3,E15.5)
  301 CONTINUE
  300 CONTINUE
C
CHAPTER  4  4  4  4  4  SOLUTION OF DIFFERENCE EQUATIONS  4  4  4  4  4
C
      DO 400 N=1,NSWPT
  400 CALL LISOLV(2,2,NI,NJ,IT,JT,T)
      RETURN
      END
C--------------------------------------------------
      SUBROUTINE CALCTE
C
CHAPTER  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/TEN/RESORK,NSWPK,URFK
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/GEOM/INDCOS,X(IZ),Y(IZ),DXEP(IZ),DXPW(IZ),DYNP(IZ),DYPS(IZ),
     1      SNS(IZ),SEW(IZ),XU(IZ),YV(IZ),R(IZ),RV(IZ)
      COMMON
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/KASE T1/UIN,TEIN,EDIN,FLOWIN,ALAMDA,
     2         RSMALL,DIAM,AL1,AL2,JSTEP,ISTEP,JSTP1,JSTM1,ISTP1,ISTM1
     1/TURB/GEN(IZ,IZ),CD,CMU,C1,C2,CAPPA,ELOG,PRED,PRTE
     1/WALLF/YPLUSN(IZ),XPLUSW(IZ),TAUN(IZ),TAUW(IZ)
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
     1/SUSP/SUKD(IZ,IZ),SPKD(IZ,IZ)
C
CHAPTER  1  1  1  1  1  1  ASSEMBLY OF COEFFICIENTS  1  1  1  1  1  1
C
      PRTE=1.0
      DO 100 I=2,NIM1
      DO 101 J=2,NJM1
C-----COMPUTE AREAS AND VOLUME
      AREAN=RV(J+1)*SEW(I)
      AREAS=RV(J)*SEW(I)
      AREAEW=R(J)*SNS(J)
      VOL=R(J)*SNS(J)*SEW(I)
C-----CALCULATE CONVECTION COEFFICIENTS
      GN=0.5*(DEN(I,J)+DEN(I,J+1))*V(I,J+1)
      GS=0.5*(DEN(I,J)+DEN(I,J-1))*V(I,J)
      GE=0.5*(DEN(I,J)+DEN(I+1,J))*U(I+1,J)
      GW=0.5*(DEN(I,J)+DEN(I-1,J))*U(I,J)
      CN=GN*AREAN
      CS=GS*AREAS
      CE=GE*AREAEW
      CW=GW*AREAEW
C-----CALCULATE DIFFUSION COEFFICIENTS
      GAMN=0.5*(VIS(I,J)+VIS(I,J+1))/PRTE
      GAMS=0.5*(VIS(I,J)+VIS(I,J-1))/PRTE
      GAME=0.5*(VIS(I,J)+VIS(I+1,J))/PRTE
      GAMW=0.5*(VIS(I,J)+VIS(I-1,J))/PRTE
      DN=GAMN*AREAN/DYNP(J)
      DS=GAMS*AREAS/DYPS(J)
      DE=GAME*AREAEW/DXEP(I)
      DW=GAMW*AREAEW/DXPW(I)
C-----SOURCE TERMS
      SMP=CN-CS+CE-CW
      CP=AMAX1(ZERO,SMP)
      CPO=CP
      DUDY=((U(I,J)+U(I+1,J)+U(I,J+1)+U(I+1,J+1))/4.-(U(I,J)+U(I+1,J)+
     1U(I,J-1)+U(I+1,J-1))/4.0)/SNS(J)
      DUDX=(U(I+1,J)-U(I,J))/SEW(I)
      DVDY=(V(I,J+1)-V(I,J))/SNS(J)
      DVDX=((V(I,J)+V(I,J+1)+V(I+1,J)+V(I+1,J+1))/4.-(V(I,J)+V(I,J+1)+V(
     1I-1,J)+V(I-1,J+1))/4.)/SEW(I)
      GEN(I,J)=(2.*(DUDX**2+DVDY**2)+(DUDY+DVDX)**2)*VIS(I,J)
      IF(J.GT.2) VDR=V(I,J)/RV(J)
      IF(J.EQ.2) VDR=0.
      IF(INDCOS.EQ.2) GEN(I,J)=GEN(I,J)+VIS(I,J)*0.5*(VDR+V(I,J+1)/
     1                RV(J+1))**2
C-----ASSEMBLE MAIN COEFFICIENTS
      AN(I,J)=AMAX1(ABS(0.5*CN),DN)-0.5*CN
      AS(I,J)=AMAX1(ABS(0.5*CS),DS)+0.5*CS
      AE(I,J)=AMAX1(ABS(0.5*CE),DE)-0.5*CE
      AW(I,J)=AMAX1(ABS(0.5*CW),DW)+0.5*CW
      SU(I,J)=CPO*TE(I,J)
      SUKD(I,J)=SU(I,J)
      SU(I,J)=SU(I,J)+GEN(I,J)*VOL
      SP(I,J)=-CP
      SPKD(I,J)=SP(I,J)
      SP(I,J)=SP(I,J)-CD*CMU*DEN(I,J)**2*TE(I,J)*VOL/VIS(I,J)
  101 CONTINUE
  100 CONTINUE
C
CHAPTER  2  2  2  2  2  2  PROBLEM MODIFICATIONS  2  2  2  2  2  2
C
      CALL MODTE
C
CHAPTER 3 FINAL COEFFICIENT ASSEMBLY AND RESIDUAL SOURCE CALCULATION 3
C
      RESORK=0.0
      DO 300 I=2,NIM1
      DO 301 J=2,NJM1
      AP(I,J)=AN(I,J)+AS(I,J)+AE(I,J)+AW(I,J)-SP(I,J)
      RESOR=AN(I,J)*TE(I,J+1)+AS(I,J)*TE(I,J-1)+AE(I,J)*TE(I+1,J)
     1      +AW(I,J)*TE(I-1,J)-AP(I,J)*TE(I,J)+SU(I,J)
      VOL=R(J)*SEW(I)*SNS(J)
      SORVOL=GREAT*VOL
      IF(-SP(I,J).GT.0.5*SORVOL) RESOR=RESOR/SORVOL
      RESORK=RESORK+ABS(RESOR)
C-----UNDER-RELAXATION
      AP(I,J)=AP(I,J)/URFK
      SU(I,J)=SU(I,J)+(1.-URFK)*AP(I,J)*TE(I,J)
  301 CONTINUE
  300 CONTINUE
C
CHAPTER  4  4  4  4  4  SOLUTION OF DIFFERENCE EQUATIONS  4  4  4  4  4
C
      DO 400 N=1,NSWPK
  400 CALL LISOLV(2,2,NI,NJ,IT,JT,TE)
      RETURN
      END
C--------------------------------------------------
      SUBROUTINE CALCU
C
CHAPTER  0  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/UVEL/RESORU,NSWPU,URFU,DXEPU(IZ),DXPWU(IZ),SEWU(IZ)
     1/PCOR/RESORM,NSWPP,URFP,DU(IZ,IZ),DV(IZ,IZ),IPREF,JPREF
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/GEOM/INDCOS,X(IZ),Y(IZ),DXEP(IZ),DXPW(IZ),DYNP(IZ),DYPS(IZ),
     1      SNS(IZ),SEW(IZ),XU(IZ),YV(IZ),R(IZ),RV(IZ)
      COMMON
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
     1/KASE T1/UIN,TEIN,EDIN,FLOWIN,ALAMDA,
     2         RSMALL,DIAM,AL1,AL2,JSTEP,ISTEP,JSTP1,JSTM1,ISTP1,ISTM1
C
C
C
CHAPTER  1  1  1  1  1  1  ASSEMBLY OF COEFFICIENTS  1  1  1  1  1  1  1
C
      DO 100 I=3,NIM1
      DO 101 J=2,NJM1
C-----COMPUTE AREAS AND VOLUME
      AREAN=RV(J+1)*SEWU(I)
      AREAS=RV(J)*SEWU(I)
      AREAEW=R(J)*SNS(J)
      VOL=R(J)*SEWU(I)*SNS(J)
C-----CALCULATE CONVECTION COEFFICIENTS
      GN=0.5*(DEN(I,J+1)+DEN(I,J))*V(I,J+1)
      GNW=0.5*(DEN(I-1,J)+DEN(I-1,J+1))*V(I-1,J+1)
      GS=0.5*(DEN(I,J-1)+DEN(I,J))*V(I,J)
      GSW=0.5*(DEN(I-1,J)+DEN(I-1,J-1))*V(I-1,J)
      GE=0.5*(DEN(I+1,J)+DEN(I,J))*U(I+1,J)
      GP=0.5*(DEN(I,J)+DEN(I-1,J))*U(I,J)
      GW=0.5*(DEN(I-1,J)+DEN(I-2,J))*U(I-1,J)
      CN=0.5*(GN+GNW)*AREAN
      CS=0.5*(GS+GSW)*AREAS
      CE=0.5*(GE+GP)*AREAEW
      CW=0.5*(GP+GW)*AREAEW
C-----CALCULATE DIFFUSION COEFFICIENTS
      VISN=0.25*(VIS(I,J)+VIS(I,J+1)+VIS(I-1,J)+VIS(I-1,J+1))
      VISS=0.25*(VIS(I,J)+VIS(I,J-1)+VIS(I-1,J)+VIS(I-1,J-1))
      DN=VISN*AREAN/DYNP(J)
      DS=VISS*AREAS/DYPS(J)
      DE=VIS(I,J)*AREAEW/DXEPU(I)
      DW=VIS(I-1,J)*AREAEW/DXPWU(I)
C-----CALCULATE COEFFICIENTS OF SOURCE TERMS
      SMP=CN-CS+CE-CW
      CP=AMAX1(ZERO,SMP)
      CPO=CP
C-----ASSEMBLE MAIN COEFFICIENTS
      AN(I,J)=AMAX1(ABS(0.5*CN),DN)-0.5*CN
      AS(I,J)=AMAX1(ABS(0.5*CS),DS)+0.5*CS
      AE(I,J)=AMAX1(ABS(0.5*CE),DE)-0.5*CE
      AW(I,J)=AMAX1(ABS(0.5*CW),DW)+0.5*CW
      DU(I,J)=AREAEW
      SU(I,J)=CPO*U(I,J)+DU(I,J)*(P(I-1,J)-P(I,J))
      SP(I,J)=-CP
      DUDXP  =(U(I+1,J)-U(I,J))/SEW(I)
      DUDXM  =(U(I,J)-U(I-1,J))/SEW(I-1)
      SU(I,J) =(VIS(I,J)*DUDXP-VIS(I-1,J)*DUDXM)/SEWU(I)*VOL+SU(I,J)
      GAMP   =0.25*(VIS(I,J)+VIS(I-1,J)+VIS(I,J+1)+VIS(I-1,J+1))
      DVDXP  =RV(J+1)*(V(I,J+1)-V(I-1,J+1))/DXPW(I)
      GAMM   =0.25*(VIS(I,J)+VIS(I-1,J)+VIS(I,J-1)+VIS(I-1,J-1))
      DVDXM  =RV(J)*(V(I,J)-V(I-1,J))/DXPW(I)
      SU(I,J) =SU(I,J)+(GAMP*DVDXP-GAMM*DVDXM)/SNS(J)/R(J)*VOL
  101 CONTINUE
  100 CONTINUE
CHAPTER  2  2  2  2  2  2  2  PROBLEM MODIFICATIONS  2  2  2  2  2  2  2
C
      CALL MODU
C
CHAPTER  3  FINAL COEFF. ASSEMBLY AND RESIDUAL SOURCE CALCULATION  3  3
      RESORU=0.0
      DO 300 I=3,NIM1
      DO 301 J=2,NJM1
      AP(I,J)=AN(I,J)+AS(I,J)+AE(I,J)+AW(I,J)-SP(I,J)
      DU(I,J)=DU(I,J)/AP(I,J)
      RESOR=AN(I,J)*U(I,J+1)+AS(I,J)*U(I,J-1)+AE(I,J)*U(I+1,J)
     1     +AW(I,J)*U(I-1,J)-AP(I,J)*U(I,J)+SU(I,J)
      VOL=R(J)*SEW(I)*SNS(J)
      SORVOL=GREAT*VOL
      IF(-SP(I,J).GT.0.5*SORVOL)  RESOR=RESOR/SORVOL
      RESORU=RESORU+ABS(RESOR)
C-----UNDER-RELAXATION
      AP(I,J)=AP(I,J)/URFU
      SU(I,J)=SU(I,J)+(1.-URFU)*AP(I,J)*U(I,J)
      DU(I,J)=DU(I,J)*URFU
  301 CONTINUE
  300 CONTINUE
C
CHAPTER  4  4  4  SOLUTION OF DIFFERENCE EQUATION  4  4  4  4  4  4  4
C
      DO 400 N=1,NSWPU
  400 CALL LISOLV(3,2,NI,NJ,IT,JT,U)
      RETURN
      END


      SUBROUTINE CALCV
C
CHAPTER  0  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/VVEL/RESORV,NSWPV,URFV,DYNPV(IZ),DYPSV(IZ),SNSV(IZ),RCV(IZ)
     1/PCOR/RESORM,NSWPP,URFP,DU(IZ,IZ),DV(IZ,IZ),IPREF,JPREF
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/GEOM/INDCOS,X(IZ),Y(IZ),DXEP(IZ),DXPW(IZ),DYNP(IZ),DYPS(IZ),
     1      SNS(IZ),SEW(IZ),XU(IZ),YV(IZ),R(IZ),RV(IZ)
      COMMON
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
     1/KASE T1/UIN,TEIN,EDIN,FLOWIN,ALAMDA,
     2         RSMALL,DIAM,AL1,AL2,JSTEP,ISTEP,JSTP1,JSTM1,ISTP1,ISTM1
C
CHAPTER  1  1  1  1  1  1  ASSEMBLY OF COEFFICIENTS  1  1  1  1  1  1  1
C
      DO 100 I=2,NIM1
      DO 101 J=3,NJM1
C-----COMPUTE AREAS AND VOLUME
      AREAN=RCV(J+1)*SEW(I)
      AREAS=RCV(J)*SEW(I)
      AREAEW=RV(J)*SNSV(J)
      VOL=RV(J)*SEW(I)*SNSV(J)
C-----CALCULATE CONVECTION COEFFICIENTS
      GN=0.5*(DEN(I,J+1)+DEN(I,J))*V(I,J+1)
      GP=0.5*(DEN(I,J)+DEN(I,J-1))*V(I,J)
      GS=0.5*(DEN(I,J-1)+DEN(I,J-2))*V(I,J-1)
      GE=0.5*(DEN(I+1,J)+DEN(I,J))*U(I+1,J)
      GSE=0.5*(DEN(I,J-1)+DEN(I+1,J-1))*U(I+1,J-1)
      GW=0.5*(DEN(I,J)+DEN(I-1,J))*U(I,J)
      GSW=0.5*(DEN(I,J-1)+DEN(I-1,J-1))*U(I,J-1)
      CN=0.5*(GN+GP)*AREAN
      CS=0.5*(GP+GS)*AREAS
      CE=0.5*(GE+GSE)*AREAEW
      CW=0.5*(GW+GSW)*AREAEW
C-----CALCULATE DIFFUSION COEFFICIENTS
      VISE=0.25*(VIS(I,J)+VIS(I+1,J)+VIS(I,J-1)+VIS(I+1,J-1))
      VISW=0.25*(VIS(I,J)+VIS(I-1,J)+VIS(I,J-1)+VIS(I-1,J-1))
      DN=VIS(I,J)*AREAN/DYNPV(J)
      DS=VIS(I,J-1)*AREAS/DYPSV(J)
      DE=VISE*AREAEW/DXEP(I)
      DW=VISW*AREAEW/DXPW(I)
C-----CALCULATE COEFFICIENTS OF SOURCE TERMS
      SMP=CN-CS+CE-CW
      CP=AMAX1(ZERO,SMP)
      CPO=CP
C-----ASSEMBLE MAIN COEFFICIENTS
      AN(I,J)=AMAX1(ABS(0.5*CN),DN)-0.5*CN
      AS(I,J)=AMAX1(ABS(0.5*CS),DS)+0.5*CS
      AE(I,J)=AMAX1(ABS(0.5*CE),DE)-0.5*CE
      AW(I,J)=AMAX1(ABS(0.5*CW),DW)+0.5*CW
      DV(I,J)=0.5*(AREAN+AREAS)
C     PN=P(I,J)+(P(I,J+1)-P(I,J))/(Y(J+1)-Y(J))*(YVN(J)-Y(J))
C     PS=P(I,J)+(P(I,J-1)-P(I,J))/(Y(J-1)-Y(J))*(YVN(J)-Y(J))
C     SU(I,J)=DV(I,J)*(PS-PN)
      SU(I,J)=CPO*V(I,J)+DV(I,J)*(P(I,J-1)-P(I,J))
      SP(I,J)=-CP
      IF(INDCOS.EQ.2) SP(I,J)=SP(I,J)-1.*VIS(I,J)*VOL/RV(J)**2
      DUDYP  =(U(I+1,J)-U(I+1,J-1))/DYPS(J)
      GAMP   =0.25*(VIS(I,J)+VIS(I+1,J)+VIS(I,J-1)+VIS(I+1,J-1))
      GAMM   =0.25*(VIS(I,J)+VIS(I-1,J)+VIS(I,J-1)+VIS(I-1,J-1))
      DUDYM  =(U(I,J)-U(I,J-1))/DYPS(J)
      SU(I,J) =SU(I,J)+(GAMP*DUDYP-GAMM*DUDYM)/SEW(I)*VOL
      DVDYP  =(V(I,J+1)-V(I,J))/SNS(J)
      RGAMP  =VIS(I,J)*R(J)
      DVDYM  =(V(I,J)-V(I,J-1))/SNS(J-1)
      RGAMM  =VIS(I,J-1)*R(J-1)
      SU(I,J) =SU(I,J)+(RGAMP*DVDYP-RGAMM*DVDYM)/(R(J)*SNSV(J))*VOL
  101 CONTINUE
  100 CONTINUE
C
CHAPTER  2  2  2  2  2  2  2  PROBLEM MODIFICATIONS  2  2  2  2  2  2  2
C
      CALL MODV
C
CHAPTER  3  FINAL COEFF. ASSEMBLY AND RESIDUAL SOURCE CALCULATION  3  3
C
      RESORV=0.0
      DO 300 I=2,NIM1
      DO 301 J=3,NJM1
      AP(I,J)=AN(I,J)+AS(I,J)+AE(I,J)+AW(I,J)-SP(I,J)
      DV(I,J)=DV(I,J)/AP(I,J)
      RESOR=AN(I,J)*V(I,J+1)+AS(I,J)*V(I,J-1)+AE(I,J)*V(I+1,J)
     1     +AW(I,J)*V(I-1,J)-AP(I,J)*V(I,J)+SU(I,J)
      VOL=R(J)*SEW(I)*SNS(J)
      SORVOL=GREAT*VOL
      IF(-SP(I,J).GT.0.5*SORVOL)  RESOR=RESOR/SORVOL
      RESORV=RESORV+ABS(RESOR)
C-----UNDER-RELAXATION
      AP(I,J)=AP(I,J)/URFV
      SU(I,J)=SU(I,J)+(1.-URFV)*AP(I,J)*V(I,J)
      DV(I,J)=DV(I,J)*URFV
  301 CONTINUE
  300 CONTINUE
C
CHAPTER  4  4  4  SOLUTION OF DIFFERENCE EQUATION  4  4  4  4  4  4  4
C
      DO 400 N=1,NSWPV
  400 CALL LISOLV(2,3,NI,NJ,IT,JT,V)
      RETURN
      END
      SUBROUTINE INIT
C
CHAPTER  0  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/UVEL/RESORU,NSWPU,URFU,DXEPU(IZ),DXPWU(IZ),SEWU(IZ)
     1/VVEL/RESORV,NSWPV,URFV,DYNPV(IZ),DYPSV(IZ),SNSV(IZ),RCV(IZ)
     1/PCOR/RESORM,NSWPP,URFP,DU(IZ,IZ),DV(IZ,IZ),IPREF,JPREF
     1/TEMPC/RESORT,URFT,ZERO,NSWPT
     1/CONCEN/RESORC,URFC,NSWPC
     1/TEN/RESORK,NSWPK,URFK
     1/TDIS/RESORE,NSWPD,URFE
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/GEOM/INDCOS,X(IZ),Y(IZ),DXEP(IZ),DXPW(IZ),DYNP(IZ),DYPS(IZ),
     1      SNS(IZ),SEW(IZ),XU(IZ),YV(IZ),R(IZ),RV(IZ)
      COMMON
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/KASE T1/UIN,TEIN,EDIN,FLOWIN,ALAMDA,
     2         RSMALL,DIAM,AL1,AL2,JSTEP,ISTEP,JSTP1,JSTM1,ISTP1,ISTM1
     1/TURB/GEN(IZ,IZ),CD,CMU,C1,C2,CAPPA,ELOG,PRED,PRTE
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
      CHARACTER*6 HEDU(6),HEDV(6),HEDP(6),HEDT(6),HEDK(6),HEDD(6),
     1HEDM(6),HEDA(6),HEDB(6)
C
CHAPTER  1  1  1  1  1  CALCULATE GEOMETRICAL QUANTITIES  1  1  1  1  1
C
      DO 100 J=1,NJ
      R(J)=Y(J)
  100 IF(INDCOS.EQ.1)R(J)=1.0
      DXPW(1)=0.0
      DXEP(NI)=0.0
      DO 101 I=1,NIM1
      DXEP(I)=X(I+1)-X(I)
  101 DXPW(I+1)=DXEP(I)
      DYPS(1)=0.0
      DYNP(NJ)=0.0
      DO 102 J=1,NJM1
      DYNP(J)=Y(J+1)-Y(J)
  102 DYPS(J+1)=DYNP(J)
      SEW(1)=0.0
      SEW(NI)=0.0
      DO 103 I=2,NIM1
  103 SEW(I)=0.5*(DXEP(I)+DXPW(I))
      SNS(1)=0.0
      SNS(NJ)=0.0
      DO 104 J=2,NJM1
  104 SNS(J)=0.5*(DYNP(J)+DYPS(J))
      XU(1)=0.0
      DO 105 I=2,NI
  105 XU(I)=0.5*(X(I)+X(I-1))
      DXPWU(1)=0.0
      DXPWU(2)=0.0
      DXEPU(1)=0.0
      DXEPU(NI)=0.0
      DO 106 I=2,NIM1
      DXEPU(I)=XU(I+1)-XU(I)
  106 DXPWU(I+1)=DXEPU(I)
      SEWU(1)=0.0
      SEWU(2)=0.0
      DO 107 I=3,NIM1
  107 SEWU(I)=0.5*(DXEPU(I)+DXPWU(I))
      YV(1)=0.0
      RV(1)=0.0
      DO 108 J=2,NJ
      RV(J)=0.5*(R(J)+R(J-1))
      RCV(J)=0.5*(RV(J)+RV(J-1))
  108 YV(J)=0.5*(Y(J)+Y(J-1))
      DYPSV(1)=0.0
      DYPSV(2)=0.0
      DYNPV(NJ)=0.0
      DO 109 J=2,NJM1
      DYNPV(J)=YV(J+1)-YV(J)
  109 DYPSV(J+1)=DYNPV(J)
      SNSV(1)=0.0
      SNSV(2)=0.0
      SNSV(NJ)=0.0
      DO 110 J=3,NJM1
  110 SNSV(J)=0.5*(DYNPV(J)+DYPSV(J))
C
CHAPTER  2  2  2  2  2  2  SET VARIABLES TO ZERO  2  2  2  2  2  2
C
C
C-----INITIAL RESIDUES SET ZERO TO AVOID INDEFINITE RESULTS
C
      RESORU=0.0
      RESORV=0.0
      RESORM=0.0
      RESORT=0.0
      RESORK=0.0
      RESORE=0.0
      RESORC=0.0
C
C
      DO 200 I=1,NI
      DO 200 J=1,NJ
      U(I,J)=0.0
      V(I,J)=0.0
      P(I,J)=1.0E5
      T(I,J)=0.0
      PP(I,J)=0.0
      TE(I,J)=0.0
      ED(I,J)=0.0
      C(I,J)=0.0
      DIFF(I,J)=VISCOS/PRANDT
      DEN(I,J)=DENSIT
      VIS(I,J)=VISCOS
      GAMH(I,J)=VISCOS/PRANDT
      DU(I,J)=0.0
      DV(I,J)=0.0
      SU(I,J)=0.0
      SP(I,J)=0.0
 200  CONTINUE
      RETURN
      END
      SUBROUTINE LISOLV(ISTART,JSTART,NII,NJJ,ITT,JTT,PHI)
C
CHAPTER  0  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      DIMENSION PHI(IZ,IZ),A(IZ),B(IZ),C(IZ),D(IZ)
      COMMON
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
      NIM1=NII-1
      NJM1=NJJ-1
      JSTM1=JSTART-1
      A(JSTM1)=0.0
C-----COMMENCE W-E SWEEP
      DO 100 I=ISTART,NIM1
      C(JSTM1)=PHI(I,JSTM1)
C-----COMMENCE S-N TRAVERSE
      DO 101 J=JSTART,NJM1
C-----ASSEMBLE TDMA COEFFICIENTS
      A(J)=AN(I,J)
      B(J)=AS(I,J)
      C(J)=AE(I,J)*PHI(I+1,J)+AW(I,J)*PHI(I-1,J)+SU(I,J)
      D(J)=AP(I,J)
C-----CALCULATE COEFFICIENTS OF RECURRENCE FORMULA
      TERM=1./(D(J)-B(J)*A(J-1))
      A(J)=A(J)*TERM
  101 C(J)=(C(J)+B(J)*C(J-1))*TERM
C-----OBTAIN NEW PHI'S
      DO 102 JJ=JSTART,NJM1
      J=NJJ+JSTM1-JJ
  102 PHI(I,J)=A(J)*PHI(I,J+1)+C(J)
  100 CONTINUE
      RETURN
      END
C------------------------------------------------------
      SUBROUTINE PRINT(ISTART,JSTART,NI,NJ,IT,JT,X,Y,PHI,HEAD)
      PARAMETER (IZ=20)
      DIMENSION PHI(IZ,IZ),X(IZ),Y(IZ),STORE(55)
      DIMENSION F(25),F4(55)
      CHARACTER*6 HEAD(6)
C     DATA F/4H(1H ,4H,A6,,4HI3, ,4H11I ,4H10, ,4H7X, ,
C    14HA6) /
C     DATA F4/4H 1I ,4H 2I ,4H 3I ,4H 4I ,4H 5I ,4H 6I ,
C    1        4H 7I ,4H 8I ,4H 9I ,4H10I ,4H11I /
C     DATA HI,HY/6H  I = , 6H Y =  /
      ISKIP=1
      JSKIP=1
      ICOUNT=0.0
      WRITE(8,110)HEAD
      ISTA=ISTART-11
  100 CONTINUE
      ISTA=ISTA+11
      IEND=ISTA+9
      IEND=MIN0(NI,IEND)
      DO 10 III=ISTA,IEND,ISKIP
  10  ICOUNT=ICOUNT+1
C     F(4)=F4(IEND-ISTA)
      WRITE(8,112)
      DO 101 JJ=JSTART,NJ,JSKIP
      J=JSTART+NJ-JJ
      DO 120 I=ISTA,IEND
      A=PHI(I,J)
      IF(ABS(A).LT.1.E-20) A=0.
120   STORE(I)=A
      IF(ICOUNT.EQ.8)THEN
      WRITE(8,113)  J,(STORE(I),I=ISTA,IEND,ISKIP),Y(J)
      ELSEIF(ICOUNT.EQ.9)THEN
      WRITE(8,115)  J,(STORE(I),I=ISTA,IEND,ISKIP),Y(J)
      ELSEIF(ICOUNT.EQ.10)THEN
      WRITE(8,116)  J,(STORE(I),I=ISTA,IEND,ISKIP),Y(J)
      ELSEIF(ICOUNT.EQ.11)THEN
      WRITE(8,117)  J,(STORE(I),I=ISTA,IEND,ISKIP),Y(J)
      ELSEIF(ICOUNT.EQ.20)THEN
      WRITE(8,200) J,(STORE(I),I=ISTA,IEND,ISKIP),Y(J)
      ENDIF
101   CONTINUE
      IF(ICOUNT.EQ.8)THEN
      WRITE(8,114) (X(I),I=ISTA,IEND,ISKIP)
      ELSEIF(ICOUNT.EQ.9)THEN
      WRITE(8,118) (X(I),I=ISTA,IEND,ISKIP)
      ELSEIF(ICOUNT.EQ.10)THEN
      WRITE(8,119) (X(I),I=ISTA,IEND,ISKIP)
      ELSEIF(ICOUNT.EQ.11)THEN
      WRITE(8,121) (X(I),I=ISTA,IEND,ISKIP)
      ELSEIF(ICOUNT.EQ.20)THEN
      WRITE(8,210) (X(I),I=ISTA,IEND,ISKIP)
      ENDIF
C------------------------------------------------
      IF(IEND.LT.NI)GO TO 100
      RETURN
  110 FORMAT(1H0,20(2H*-),7X,9A4,20(2H*-))
  112 FORMAT(3H  J)
  113 FORMAT(1H ,I5,8E15.3,F9.4)
  114 FORMAT(1H0,'X=    ',8F9.4)
  115 FORMAT(1H ,I5,9E15.3,F9.4)
  116 FORMAT(1H ,I5,10E15.3,F9.4)
  117 FORMAT(1H ,I5,11E15.3,F9.4)
  200 FORMAT(1H ,I5,20E15.3,F9.4)
  118 FORMAT(1H0,'X=    ',9F15.4)
  119 FORMAT(1H0,'X=    ',10F15.4)
  121 FORMAT(1H0,'X=    ',11F15.4)
  210 FORMAT(1H0,'X=    ',20F15.4)
      END
C-----------------------
      SUBROUTINE PROMOD
C
CHAPTER  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/UVEL/RESORU,NSWPU,URFU,DXEPU(IZ),DXPWU(IZ),SEWU(IZ)
     1/VVEL/RESORV,NSWPV,URFV,DYNPV(IZ),DYPSV(IZ),SNSV(IZ),RCV(IZ)
     1/PCOR/RESORM,NSWPP,URFP,DU(IZ,IZ),DV(IZ,IZ),IPREF,JPREF
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/GEOM/INDCOS,X(IZ),Y(IZ),DXEP(IZ),DXPW(IZ),DYNP(IZ),DYPS(IZ),
     1      SNS(IZ),SEW(IZ),XU(IZ),YV(IZ),R(IZ),RV(IZ)
      COMMON
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/KASE T1/UIN,TEIN,EDIN,FLOWIN,ALAMDA,
     2         RSMALL,DIAM,AL1,AL2,JSTEP,ISTEP,JSTP1,JSTM1,ISTP1,ISTM1
     1/SUSP/SUKD(IZ,IZ),SPKD(IZ,IZ)
     1/COEF/AP(IZ,IZ),AN(IZ,IZ),AS(IZ,IZ),AE(IZ,IZ),AW(IZ,IZ),SU(IZ,IZ),
     1      SP(IZ,IZ)
     1/TURB/GEN(IZ,IZ),CD,CMU,C1,C2,CAPPA,ELOG,PRED,PRTE
     1/WALLF/YPLUSN(IZ),XPLUSW(IZ),TAUN(IZ),TAUW(IZ)
C
CHAPTER 0.5  0.5  0.5  0.5  0.5  CONCENTRATION 0.5  0.5  0.5  0.5
      ENTRY MODC
      RETURN
CHAPTER  1  1  1  1  1  1  1  1  PROPERTIES  1  1  1  1  1  1  1  1  1
C
      ENTRY MODPRO
C-----NO MODIFICATIONS FOR THIS PROBLEM
      RETURN
C
CHAPTER  2  2  2  2  2  2  2  2  U MOMENTUM  2  2  2  2  2  2  2  2  2
C
      ENTRY MODU
C-----TOP WALL
      CDTERM=CMU**0.25
      YP=YV(NJ)-Y(NJM1)
      J=NJM1
      DO 210 I=2,NIM1
      SQRTK=SQRT(0.5*(TE(I,J)+TE(I-1,J)))
      DENU=0.5*(DEN(I,J)+DEN(I-1,J))
      YPLUSA=0.5*(YPLUSN(I)+YPLUSN(I-1))
      IF(YPLUSA.LE.11.63) GOTO 211
      TMULT=DENU*CDTERM*SQRTK*CAPPA/ALOG(ELOG*YPLUSA)
      GOTO 212
  211 TMULT=VISCOS/YP
  212 TAUN(I)=-TMULT*U(I,J)
      SP(I,J)=SP(I,J)-TMULT*SEWU(I)*RV(NJ)
  210 AN(I,J)=0.0
      TAUN(2)=TAUN(3)
      TAUN(NI)=TAUN(NIM1)
C-----SYMMETRY AXIS
      DO 203 I=1,NI
      U(I,1)=U(I,2)
  203 AS(I,2)=0.0
C-----OUTLET PLANE
      ARDENT=0.0
      FLOW=0.0
      DO 204 J=2,NJM1
      ARDEN=0.5*(DEN(NIM1,J)+DEN(NIM1-1,J))*SNS(J)*R(J)
      ARDENT=ARDENT+ARDEN
  204 FLOW=FLOW+ARDEN*U(NIM1,J)
      UINC=(FLOWIN-FLOW)/ARDENT
      DO 205 J=2,NJM1
  205 U(NI,J)=U(NIM1,J)+UINC
      RETURN
C
CHAPTER  3  3  3  3  3  3  3  3  V MOMENTUM  3  3  3  3  3  3  3  3  3
C
      ENTRY MODV
C-----TOP WALL
      DO 313 I=2,NIM1
  313 AN(I,NJM1)=0.0
C-----SYMMETRY AXIS
      DO 302 I=2,NIM1
  302 AS(I,3)=0.0
      RETURN
C
CHAPTER  4  4  4  4  4  4  PRESSURE CORRECTION  4  4  4  4  4  4  4  4
C
      ENTRY MODP
      RETURN
C
CHAPTER  5  5  5  5  5  5  5  THERMAL ENERGY  5  5  5  5  5  5  5  5  5
C
      ENTRY MODT
C-----TOP WALL
      TWALL=293.
      CDTERM=CMU**0.25
      J=NJM1
      ITEMP=NI/2
      DO 216 I=ITEMP,NIM1
      AN(I,J)=0.0
      YP=YV(NJ)-Y(NJM1)
C      SQRTK=SQRT(0.5*(TE(I,J)+TE(I-1,J)))
C      DENT=DEN(I,J)
C      YPLUSA=0.5*(YPLUSN(I)+YPLUSN(I-1))
C      PRTUB=PRANDT*VIS(I,J)/VISCOS
C      PFUN=9.24*((PRANDT/PRTUB)**0.75+1.)
C      IF(YPLUSA.LE.11.63) GOTO 214
C      UPLUSA=1./CAPPA*ALOG(ELOG*YPLUSA)
C      TPLUSA=PRTUB*(UPLUSA+PFUN)
C      X1=DENT*CDTERM*SQRTK*(T(I,J)-TWALL)*SEW(I)*R(I)/TPLUSA
C      WRITE(*,217)X1
 217  FORMAT(5X,'    X1= ',E15.5)
C      SU(I,J)=SU(I,J)-X1
C      GOTO 216
 214  UPLUSA=YPLUSA
      X1=VIS(I,J)/PRANDT*(T(I,J)-TWALL)*SEW(I)*R(I)/YP
      WRITE(*,217)X1
      SU(I,J)=SU(I,J)-X1
 216  CONTINUE

C-----SYMMETRY
      J=2
      DO 220 I=2,NIM1
      T(I,1)=T(I,J)
 220  AS(I,J)=0.0
C-----OUTLET
      I=NIM1
      DO 215 J=2,NJM1
C     T(NI,J)=T(I,J)
  215 AE(I,J)=0.0
      RETURN
C
CHAPTER  6  6  6  6  6  6  6  TURBULENT KINETIC ENERGY 6  6  6  6  6  6
C
      ENTRY MODTE
C-----TOP WALL
      CDTERM=CMU**0.25
      YP=YV(NJ)-Y(NJM1)
      J=NJM1
      DO 610 I=2,NIM1
      DENU=DEN(I,J)
      SQRTK=SQRT(TE(I,J))
      VOL=R(J)*SNS(J)*SEW(I)
      GENCOU=0.5*(ABS(TAUN(I+1)*U(I+1,J))+ABS(TAUN(I)*U(I,J)))/YP
      YPLUSN(I)=DENU*CDTERM*SQRTK*YP/VISCOS
      DUDY=((U(I,J)+U(I+1,J)+U(I,J+1)+U(I+1,J+1))/4.-(U(I,J)+U(I+1,J)+
     1U(I,J-1)+U(I+1,J-1))/4.0)/SNS(J)
      GENRES=GEN(I,J)-VIS(I,J)*DUDY*DUDY
      GEN(I,J)=GENRES+GENCOU
      IF(YPLUSN(I).LE.11.63) GOTO 611
      DITERM=DEN(I,J)*(CMU**0.75)*SQRTK*ALOG(ELOG*YPLUSN(I))/(CAPPA*YP)
      GOTO 612
  611 CONTINUE
      DITERM=DEN(I,J)*(CMU**0.75)*SQRTK*YPLUSN(I)/YP
  612 CONTINUE
      SU(I,J)=GEN(I,J)*VOL+SUKD(I,J)
      SP(I,J)=-DITERM*VOL+SPKD(I,J)
  610 AN(I,J)=0.0
C-----SYMMETRY AXIS
      J=2
      DO 630 I=2,NIM1
      TE(I,1)=TE(I,2)
      DUDY=((U(I,J)+U(I+1,J)+U(I,J+1)+U(I+1,J+1))/4.-(U(I,J)+U(I+1,J)+
     1U(I,J-1)+U(I+1,J-1))/4.)/SNS(J)
      VOL=R(J)*SNS(J)*SEW(I)
      GEN(I,J)=GEN(I,J)-VIS(I,J)*DUDY*DUDY
      SU(I,J)=SUKD(I,J)+GEN(I,J)*VOL
  630 AS(I,2)=0.0
      RETURN
C
CHAPTER  7  7  7  7  7  7  7 DISSSIPATION  7  7  7  7  7  7  7  7  7  7
C
      ENTRY MODED
C-----TOP WALL
      YP=YV(NJ)-Y(NJM1)
      J=NJM1
      TERM=(CMU**0.75)/(CAPPA*YP)
      DO 710 I=2,NIM1
      SU(I,J)=GREAT*TERM*TE(I,J)**1.5
  710 SP(I,J)=-GREAT
C-----SYMMETRY AXIS
      DO 730 I=2,NIM1
  730 AS(I,2)=0.0
      RETURN
      END
C-------------------------------------------------------------

      SUBROUTINE PROPS
C
CHAPTER  0  0  0  0  0  0  0  0  PRELIMINARIES  0  0  0  0  0  0  0  0
C
C
      PARAMETER (IZ=20)
      COMMON
     1/VAR/U(IZ,IZ),V(IZ,IZ),P(IZ,IZ),PP(IZ,IZ),TE(IZ,IZ),ED(IZ,IZ)
     2    ,T(IZ,IZ),RCON,PRESS
     1/ALL/IT,JT,NI,NJ,NIM1,NJM1,GREAT
     1/FLUPR/URFVIS,VISCOS,DENSIT,PRANDT,DEN(IZ,IZ),VIS(IZ,IZ)
     2       ,GAMH(IZ,IZ),C(IZ,IZ),DIFF(IZ,IZ),DI,URFDEN
     1/TURB/GEN(IZ,IZ),CD,CMU,C1,C2,CAPPA,ELOG,PRED,PRTE
C
CHAPTER   1   1   1   VISCOSITY AND GAMH  1  1  1
C
      DO 100 I=1,NIM1
      VIS(I,1)=VIS(I,2)
      GAMH(I,1)=GAMH(I,2)
      DO 100 J=2,NJM1
      VISOLD=VIS(I,J)
      IF(ED(I,J).EQ.0.) GOTO 102
      VIS(I,J)=DEN(I,J)*TE(I,J)**2*CMU/ED(I,J)+VISCOS
      GOTO 101
  102 VIS(I,J)=VISCOS
  101 VIS(I,J)=URFVIS*VIS(I,J)+(1.-URFVIS)*VISOLD
      GAMH(I,J)=VISCOS/PRANDT+(VIS(I,J)-VISCOS)/PRTE
C-----UNDER-RELAX VISCOSITY AND GAMH
  100 CONTINUE
C
CHAPTER 2 2 2 2 2 2 2 DENSITY
      END
C---------------------------------------------------

